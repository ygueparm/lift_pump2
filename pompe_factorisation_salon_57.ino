
#include "config.h"
#include "pompe.h"
#include "capteurs.h"
#include "securite.h"
//#include <Arduino.h>
#include <Preferences.h>
#include <esp_task_wdt.h>
#include "web_server.h"
//#define TEST   // laisser pour les test sur planche a pain
#undef TEST  // a activer pour la  production

#include <WiFi.h>
#include <DNSServer.h>
#include <ESPAsyncWebServer.h>
#include "LittleFS.h"
// initialisation serveur AP
String contenuTableau;
DNSServer dnsServer;
AsyncWebServer server(80);  // D√©claration du serveur sur le port 80

//instance pour sauvegarder les donn√©es
Preferences prefsPompe;
Preferences prefsnewTime1;  // pour stocker le temps de focntionnement de la pompe
unsigned long dernierDemarrageRecupere = 0;
// Prototype de la fonction chargerTableau()
void chargerTableau();
void setup() {
  Serial.begin(115200);
  Serial.println("\n[*] Creating AP");
  WiFi.mode(WIFI_AP);
  WiFi.softAP(ssid, password, 13);  // Utilisation du canal 10
  Serial.print("[+] AP Created with IP Gateway ");
  Serial.println(WiFi.softAPIP());
  //resistance de tirage pour les entr√©e :
  pinMode(CAPTEUR_NIVEAU_HAUT, INPUT_PULLDOWN);
  pinMode(RELAIS_SECURITE, INPUT_PULLDOWN);
  pinMode(CAPTEUR_CONTACTEUR, INPUT_PULLDOWN);
  pinMode(MOTEUR_POMPE, OUTPUT);
  digitalWrite(MOTEUR_POMPE, HIGH);
  // Configure le DNS local avec le serveur AP
  //**** affiche le tableau des dernier demarrages
  // Configure le DNS local avec le serveur AP
  dnsServer.start(53, apHostname, WiFi.softAPIP());

  //pour enregistrer les donn√©s
  prefsPompe.begin("pompe", false);  // Nom de namespace "pompe"
  // initialisation des preference pour le temps de fonctionnement
  prefsnewTime1.begin("newTime1", false);

  //configuration watchdog
  esp_task_wdt_init(WDT_TIMEOUT, true);  //enable panic so ESP32 restarts
  esp_task_wdt_add(NULL);                //add current thread to WDT watch

//initilaisation de LittleFS pour les log
 if (!LittleFS.begin(true)) {
        Serial.println("Erreur lors de l'initialisation de LittleFS");
        return;
    }
    Serial.println("LittleFS initialis√© avec succ√®s");


  chargerTableau();  //charger les donn√©es sauvegard√© au demarrage
  Serial.print("‚¨ÖÔ∏è Chargement de indice_tableau apr√®s reboot : ");
  Serial.println(indiceDerniereEntreeTempsDepuis);

  // Initialisation des variables
  initialiserVariables();
  initialiserLed();

  // lancement du serveur web
  initWebServer();
  server.begin();
    // V√©rifier si le fichier existe, sinon le cr√©er
    if (!LittleFS.exists("/compteurs.txt")) {
        Serial.println("Fichier compteurs.txt absent, cr√©ation en cours...");
        File file = LittleFS.open("/compteurs.txt", FILE_WRITE);
        if (file) {
            file.close();
            Serial.println("Fichier compteurs.txt cr√©√© avec succ√®s.");
        } else {
            Serial.println("Erreur lors de la cr√©ation du fichier compteurs.txt !");
        }
    }
}
void loop() {
  dnsServer.processNextRequest();  //pour permettre au serveur DNS de traiter les requ√™tes entrantes de mani√®re continue.
  //reveil du watchdog
  esp_task_wdt_reset();
  unsigned long currentMillisled = millis();
  //recuperation de la valeur sauvegard√© du temps de fonctionnement moteur
  TEMP_FONCTIONNEMENT_MOTEUR = prefsnewTime1.getULong("timeValue", 0) * 1000;
  capteurs = lireCapteurs();  // Rafra√Æchir les valeurs avant le test

  //gestion du clignotement de la led sans utiliser delayüåûüåû
  unsigned long intervalActuel = (capteurBloque || relaisDeclenche) ? intervalled / 6 : intervalled;

  if (moteurEnMarche && !capteurBloque || moteurEnMarche && !relaisDeclenche) {
    digitalWrite(Led, HIGH);  // Allumer la LED en continu quand le moteur est en marche et le capteur n'est pas bloqu√©
  } else {
    if (currentMillisled - previousMillisled >= intervalActuel) {
      previousMillisled = currentMillisled;  // Mise √† jour du dernier changement
      ledState = !ledState;                  // Inversion de l'√©tat de la LED
      digitalWrite(Led, ledState);           // Mise √† jour de la LED
    }
  }

  // üëøüëøsi il y a trop de securit√© on arrete l'esp32, ouais carrement peut etre a modifier vers une jolie page
  if (!verifierSecurite()) {
    return;
  }
  // debug sans utiliser de delay
  if (currentMillisled - previousMillis_print >= interval_print) {
    previousMillis_print = currentMillisled;  // Mise √† jour du dernier temps

    // Affichage des informations
    Serial.print("void loop √âtat du capteur de niveau haut : ");
    Serial.println(capteurs.niveauHaut);
    Serial.print("void loop √âtat contacteur : ");
    Serial.println(capteurs.etatContacteur);
    Serial.print("void loop √âtat du relais s√©curit√© : ");
    Serial.println(capteurs.etatRelaisSecurite);
    Serial.print("void loop moteur en marche : ");
    Serial.println(moteurEnMarche);
    Serial.print("etat si le capteur a √©t√© bloqu√© ");
    Serial.println(capteurBloque);
    lireCompteurs();
  }
  // D√©marrage du moteur de la pompeüòç
  if (peutDemarrerPompe(capteurs) && !moteurEnMarche && !capteurBloque) {
    activerPompe();
    moteurEnMarche = true;
    tempsDebut = capteurs.tempsActuel;
    tempsRelaisDeclenche = 0;  // R√©initialise le temps de d√©clenchement du relais de s√©curit√©
    tempsReel = 0;             // Mise √† z√©ro du temps de fonctionnement r√©el
    tempsNiveauHautActif = 0;  // R√©initialise le temps o√π niveauHaut est actif
    relaisDeclenche = false;   // R√©initialise l'√©tat du relais d√©clench√©
    Serial.println("D√©marrage du moteur.");
  } else {
    if (moteurEnMarche) {  // verification si le capteur niveau haut ne reste pas bloqu√©
      if (capteurs.niveauHaut == HIGH) {
        if (debutNiveauHaut == 0) {
          debutNiveauHaut = millis();  // Premier passage en HIGH
        } else if (millis() - debutNiveauHaut >= SEUIL_BLOCAGE) {
          capteurBloque = true;
          Serial.println("‚ö†Ô∏è Capteur niveauHaut bloqu√© d√©tect√© !");
          debutNiveauHaut = 0;  // Reset pour √©viter r√©p√©tition du message
        }
      } else {
        debutNiveauHaut = 0;  // R√©initialiser si le capteur repasse √† LOW
      }

      // Gestion s√©curit√© lorsque le moteur fonctionne üòñüòñ
      if (capteurs.etatRelaisSecurite == HIGH) {
        desactiverPompe();
        moteurEnMarche = false;
        relaisDeclenche = true;
        tempsRelaisDeclenche = capteurs.tempsActuel;  // Enregistre le moment du d√©clenchement
        // Incr√©menter le compteur de s√©curit√©
        securiteDeclenche++;  //incremente le nombre de fois ou cela augmente
        Serial.println("Relais de s√©curit√© d√©clench√©.");
      }
      // Arr√™t automatique apr√®s un certain tempsüíìüíì
      else if (capteurs.tempsActuel - tempsDebut >= TEMP_FONCTIONNEMENT_MOTEUR) {
        desactiverPompe();
        moteurEnMarche = false;
        Serial.println("Arr√™t automatique du moteur apr√®s un certain temps");

        // Calcul du temps √©coul√© depuis le dernier d√©marrage
        unsigned long tempsEcoule_1 = capteurs.tempsActuel - dernierDemarrage;
        uint32_t jours = tempsEcoule_1 / JOUR;
        uint32_t heures = (tempsEcoule_1 % JOUR) / HEURE;
        uint32_t minutes = ((tempsEcoule_1 % JOUR) % HEURE) / MINUTE;

        // Affichage du temps √©coul√©
        Serial.print("Dernier d√©marrage il y a ");
        Serial.print(jours);
        Serial.print("j ");
        Serial.print(heures);
        Serial.print(":");
        if (minutes < 10) {
          Serial.print("0");
        }
        Serial.println(minutes);

        // Sauvegarde des donn√©esüì•üì•
        if (tempsDebut != 0) {
          tempsDepuisDernierDemarrage[indiceDerniereEntreeTempsDepuis] = tempsEcoule_1;
          indiceDerniereEntreeTempsDepuis = (indiceDerniereEntreeTempsDepuis + 1) % MAX_ENTRIES;
          Serial.print("Indice tableau temps depuis dernier d√©marrage : ");
          Serial.println(indiceDerniereEntreeTempsDepuis);
        }

        // R√©initialisation des variables
        dernierDemarrage = capteurs.tempsActuel;                    // Met √† jour le temps du dernier d√©marrage
        prefsPompe.putULong("dernierDemarrage", dernierDemarrage);  // Sauvegarde le dernier d√©marrage dans prefs
        sauvegarderTableau();

        // R√©initialiser les compteurs li√©s au capteur
        tempsNiveauHautActif = 0;
      }
    }
  }


    // Mettre √† jour les compteurs uniquement si l'√©tat change
    compteurNiveauHaut += (capteurs.niveauHaut && !precedentNiveauHaut) ? 1 : 0;
    compteurContacteur += (capteurs.etatContacteur && !precedentContacteur) ? 1 : 0;
    compteurRelaisSecurite += (capteurs.etatRelaisSecurite && !precedentRelaisSecurite) ? 1 : 0;
    compteurMoteurEnMarche += (moteurEnMarche && !precedentMoteurEnMarche) ? 1 : 0;
    compteurCapteurBloque += (capteurBloque && !precedentCapteurBloque) ? 1 : 0;
   // Mettre √† jour les √©tats pr√©c√©dents
    precedentNiveauHaut = capteurs.niveauHaut;
    precedentContacteur = capteurs.etatContacteur;
    precedentRelaisSecurite = capteurs.etatRelaisSecurite;
    precedentMoteurEnMarche = moteurEnMarche;
    precedentCapteurBloque = capteurBloque;
   // Enregistrer les compteurs dans LittleFS (optionnel, √† faire moins fr√©quemment)
    static unsigned long dernierEnregistrement = 0;
    if (millis() - dernierEnregistrement > 60000) {  // Enregistrer toutes les 60 secondes
        enregistrerCompteurs(compteurNiveauHaut, compteurContacteur, compteurRelaisSecurite, compteurMoteurEnMarche, compteurCapteurBloque);
        dernierEnregistrement = millis();
    }
  
  
}

void sauvegarderDonnees(const char* key, const uint32_t value) {
  prefsPompe.putULong(key, value);
}

uint32_t chargerDonnees(const char* key, uint32_t defaultValue = 0) {
  return prefsPompe.getULong(key, defaultValue);
}

void sauvegarderTableau() {
  for (int i = 0; i < MAX_ENTRIES; i++) {
    sauvegarderDonnees(("temps_" + String(i)).c_str(), tempsDepuisDernierDemarrage[i]);
  }
  sauvegarderDonnees("indice_tableau", indiceDerniereEntreeTempsDepuis);
  sauvegarderDonnees("dernierDemarrage", dernierDemarrage);

  Serial.println("Donn√©es du tableau sauvegard√©es avec succ√®s.");
}

void chargerTableau() {
  for (int i = 0; i < MAX_ENTRIES; i++) {
    tempsDepuisDernierDemarrage[i] = chargerDonnees(("temps_" + String(i)).c_str());
  }
  indiceDerniereEntreeTempsDepuis = chargerDonnees("indice_tableau");
  dernierDemarrage = chargerDonnees("dernierDemarrage");
  Serial.println("Donn√©es du tableau charg√©es avec succ√®s.");
}

//ecrire l'ensemble du fonctionnement de l'esp32 
void enregistrerCompteurs(int niveauHaut, int etatContacteur, int etatRelaisSecurite, int moteurEnMarche, int capteurBloque) {
    // Ouvrir le fichier en mode √©criture (√©crase le fichier existant)
    File file = LittleFS.open("/compteurs.txt", FILE_WRITE);
    if (!file) {
        Serial.println("Erreur lors de l'ouverture du fichier compteurs pour ecrire");
        return;
    }

    // √âcrire les compteurs dans le fichier
    file.printf("Niveau haut: %d\nContacteur: %d\nRelais s√©curit√©: %d\nMoteur en marche: %d\nCapteur bloqu√©: %d\n",
                niveauHaut, etatContacteur, etatRelaisSecurite, moteurEnMarche, capteurBloque);

    // Fermer le fichier
    file.close();
    Serial.println("Compteurs enregistr√©s");
}

//lire le fichier de log dans la console serie
void lireCompteurs() {
    // Ouvrir le fichier en mode lecture
    File file = LittleFS.open("/compteurs.txt", FILE_READ);
    if (!file) {
        Serial.println("Erreur lors de l'ouverture du fichier compteurs pour lire");
        return;
    }

    // Lire et afficher le contenu du fichier
    Serial.println("Compteurs :");
    while (file.available()) {
        Serial.write(file.read());
    }

    // Fermer le fichier
    file.close();
}